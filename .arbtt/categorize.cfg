-- inactivity
$screensaver ==> tag inactive,
$idle > 60 && $desktop == "1:irc" ==> tag inactive,
$idle > 60 && current window $program == "google-chrome" && current window $title =~ m|^chrome://newtab| ==> tag inactive,

-- generic, for filtering purposes
tag Program:$current.program,
tag Desktop:$desktop,

-- unambiguous activities
$desktop == "1:irc" && current window $title =~ /^t\[N\] / ==> tag Activity:IRC,
$desktop == "1:irc" && current window $title =~ /^t\[m\] / ==> tag Activity:Mail,
$desktop =~ /:steam$/ ==> tag Activity:Games,
current window $program == "liferea" ==> tag Activity:Web-RSS,
current window $program == "google-chrome" ==> {
	current window $title =~ m|:: https?://meet\.google| ==> tag Activity:Call,
	current window $title =~ m|:: https?://.*muni.*/discussion/| ==> tag Activity:Web-Plkarna,
	current window $title =~ m|:: https?://news\.ycombinator| ==> tag Activity:Web-HN,
	current window $title =~ m!( / Twitter|\bFacebook) (::|-) ! ==> tag Activity:Web-Social,
	current window $title =~ m!:: https?://.*(freevid|porn|xvideos|xhamster)! ==> tag Activity:Watch-Porn,
	current window $title =~ m!\bYouTube (::|-) ! ==> tag Activity:Watch,
},
current window $program == "gl" ==> {
	current window $title =~ m/(?i)\bS\d\dE\d\d\b.* - mpv$/ ==> tag Activity:Watch-Series,
	current window $title =~ /- mpv$/ ==> tag Activity:Watch,
},

-- possibly ambiguous fallback activities
current window $program == "google-chrome" ==> {
	current window $title =~ m|:: https?://github| ==> tag Activity:Web-GitHub,
	$desktop == "2:web" ==> tag Activity:Web,
},

{-
-- dump uncategorized samples:
arbtt-stats  --filter='$date>='$(date +"%Y-%m-%d") \
	$(perl -Mfeature=say -ne 'say "--exclude=$1" while (/\b(Activity:[\w_-]+)/g)' ~/.arbtt/categorize.cfg) \
	--dump-samples | fgrep '(*)' | sort | uniq --count | sort -n
-}

-- vim:set ft=haskell noet:
